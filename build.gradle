plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'info.itsthesky'
def type = 'beta'
def num = '3'
version = '4.0-' + type + num

repositories {
    mavenCentral()
    jcenter()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://jitpack.io' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url = 'https://m2.dv8tion.net/releases' }

}

dependencies {
    shadow 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    shadow 'com.github.SkriptLang:Skript:2.6.1'

    // Commits: com.github.dv8fromtheworld:jda
    // Stable: net.dv8tion:JDA
    implementation('com.github.dv8fromtheworld:JDA:5.0.0-alpha.11') {
        exclude module: "opus-java"
    }
    implementation 'com.github.simplix-softworks:simplixstorage:3.2.4'
}

shadowJar {
    archiveName = 'DiSky ' + version + '.jar'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileJava.options.encoding = 'UTF-8'